# Maintainer: robiha <hallin.rle@gmail.com>

pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.r23.gdb22360
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libx11' 'libxinerama' 'libxcb' 'libxft' 'freetype2' 'otf-fira-mono')
optdepends=(
	'st: simple terminal'
	'dmenu: application launcher'
	'rofi: application launcher'
)
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("git://git.suckless.org/${_pkgname}"
	"my-config.diff"
	"dwm.desktop")

md5sums=('SKIP'
         '0c073873dcac9c22a16054cc7feb2074'
         '939f403a71b6e85261d09fc3412269ee')

pkgver() {
	cd "${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"

	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			# add all patches present in source array
			echo "Applying patch $(basename $file)..."
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${_pkgname}/README"
	install -Dm644 "${srcdir}/dwm.desktop" "${pkgdir}/usr/share/xsessions/dwm.desktop"
}
