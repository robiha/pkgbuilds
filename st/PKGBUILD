# Maintainer: robiha <hallin.rel@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.r41.g1f24bde
pkgrel=1
epoch=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft' 'otf-fira-mono')
makedepends=('ncurses' 'libxext' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("git://git.suckless.org/${_pkgname}"
	"my-config.diff"
	"st.desktop"
	"st.svg")

md5sums=('SKIP'
         '3e8065b0f6c866a49c84518e2cf2c54c'
         'fb3a8e83f84f18aec09e89a60569f086'
         'ebe50386dbdefbc6fd171fdf3120f90c')

pkgver() {
	cd "${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"
	
	# Skip terminfo which conflicts with ncurses
	sed -i '/tic /d' Makefile

	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			# add all patches present in source array
			echo "Applying patch $(basename $file)..."
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done

	# Needed for the icon patch
	# cp "${srcdir}/icon.argb" ./

}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${_pkgname}/README"
	install -Dm644 "${srcdir}/st.desktop" "${pkgdir}/usr/share/applications/st.desktop"
	install -Dm644 "${srcdir}/st.svg"  "${pkgdir}/usr/share/st/st.svg"
}
