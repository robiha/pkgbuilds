# Maintainer: robiha <hallin.rel@gmail.com>

pkgname=dmenu-git
_pkgname=dmenu
pkgver=4.7.r6.g8895128
pkgrel=1
epoch=1
pkgdesc='A generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxinerama' 'libxft' 'otf-fira-mono')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("git://git.suckless.org/${_pkgname}")

md5sums=('SKIP')

pkgver() {
	cd "${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	local file
	cd "${_pkgname}"
	
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
		elif [[ "$file" == *.diff || "$file" == *.patch ]]; then
			# add all patches present in source array
			echo "Applying patch $(basename $file)..."
			patch -Np1 <"$srcdir/$(basename ${file})"
		fi
	done
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
